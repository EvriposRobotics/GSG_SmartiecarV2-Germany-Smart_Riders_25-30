/////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
██████╗ ██╗███╗   ██╗     █████╗ ███████╗███████╗██╗ ██████╗ ███╗   ██╗███╗   ███╗███████╗███╗   ██╗████████╗
██╔══██╗██║████╗  ██║    ██╔══██╗██╔════╝██╔════╝██║██╔════╝ ████╗  ██║████╗ ████║██╔════╝████╗  ██║╚══██╔══╝
██████╔╝██║██╔██╗ ██║    ███████║███████╗███████╗██║██║  ███╗██╔██╗ ██║██╔████╔██║█████╗  ██╔██╗ ██║   ██║
██╔═══╝ ██║██║╚██╗██║    ██╔══██║╚════██║╚════██║██║██║   ██║██║╚██╗██║██║╚██╔╝██║██╔══╝  ██║╚██╗██║   ██║
██║     ██║██║ ╚████║    ██║  ██║███████║███████║██║╚██████╔╝██║ ╚████║██║ ╚═╝ ██║███████╗██║ ╚████║   ██║
╚═╝     ╚═╝╚═╝  ╚═══╝    ╚═╝  ╚═╝╚══════╝╚══════╝╚═╝ ╚═════╝ ╚═╝  ╚═══╝╚═╝     ╚═╝╚══════╝╚═╝  ╚═══╝   ╚═╝
*/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////

// button                      = D4
// Ultrasonic F,R,L            = A0-A2
// I2C Bus                     = A4-A5 -> LCD, Gyro
// MotorSpeed                  = D5
// Moto direction              = D6
// Servo steering (pwm signal) = D3

///////////////////////////////////////////////////////////////
/*
██╗     ██╗██████╗ ██████╗  █████╗ ██████╗ ██╗███████╗███████╗
██║     ██║██╔══██╗██╔══██╗██╔══██╗██╔══██╗██║██╔════╝██╔════╝
██║     ██║██████╔╝██████╔╝███████║██████╔╝██║█████╗  ███████╗
██║     ██║██╔══██╗██╔══██╗██╔══██║██╔══██╗██║██╔══╝  ╚════██║
███████╗██║██████╔╝██║  ██║██║  ██║██║  ██║██║███████╗███████║
╚══════╝╚═╝╚═════╝ ╚═╝  ╚═╝╚═╝  ╚═╝╚═╝  ╚═╝╚═╝╚══════╝╚══════╝
*/
///////////////////////////////////////////////////////////////

// Servo section libraries
#include <Servo.h>
//-------------------------------------------------

// RGB libraries
#include <Wire.h>
#include "rgb_lcd.h"

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
 ██████╗ ██╗      ██████╗ ██████╗  █████╗ ██╗         ██╗   ██╗ █████╗ ██████╗ ██╗ █████╗ ██████╗ ██╗     ███████╗███████╗
██╔════╝ ██║     ██╔═══██╗██╔══██╗██╔══██╗██║         ██║   ██║██╔══██╗██╔══██╗██║██╔══██╗██╔══██╗██║     ██╔════╝██╔════╝
██║  ███╗██║     ██║   ██║██████╔╝███████║██║         ██║   ██║███████║██████╔╝██║███████║██████╔╝██║     █████╗  ███████╗
██║   ██║██║     ██║   ██║██╔══██╗██╔══██║██║         ╚██╗ ██╔╝██╔══██║██╔══██╗██║██╔══██║██╔══██╗██║     ██╔══╝  ╚════██║
╚██████╔╝███████╗╚██████╔╝██████╔╝██║  ██║███████╗     ╚████╔╝ ██║  ██║██║  ██║██║██║  ██║██████╔╝███████╗███████╗███████║
 ╚═════╝ ╚══════╝ ╚═════╝ ╚═════╝ ╚═╝  ╚═╝╚══════╝      ╚═══╝  ╚═╝  ╚═╝╚═╝  ╚═╝╚═╝╚═╝  ╚═╝╚═════╝ ╚══════╝╚══════╝╚══════╝
*/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// timer
unsigned long start_time;
//--------------------------------------------------

// LCD connections
int Button = 4;
int colorRed = 255;
int colorGreen = 0;
int colorBlue = 0;
// LCD screen itself
rgb_lcd lcd;

// corners/edge detection
int corners;

// own variables for distances
// int Distance;
int Distance_Front;
int Distance_Left;
int Distance_Right;

float angle;
float danger;
float correction_Left = -5.0;
float correction_Right = 15.0;
float StraightAngle = 0.0;

// Driving direction 'U' for Unknown
char DrivingDirection = 'U';

// Speeds
int NormalSpeed = 100;
int SlowSpeed = 90;
int CurveSpeed = 100;
int StartSpeed = 100;

// obstacle block 'U' for unknown
char Block = 'U';

// Block variables
char P_color = 'U'; // Unknown
char P_wall_L = 'N';
char P_wall_R = 'N';
int P_x = 0;
int P_height = 0;

// last counted corners
unsigned long LastCurveTime;
unsigned long NextCurveDelay = 5000;

// both allignments (L/R)
int walldistance = 40;

// own Module
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\DCmotor.h"
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\gyro2.h"
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\steering.h"
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\ultrasonic_urm09.h"
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\raspi.h"
#include "C:\Users\WRO_FE2\Desktop\GSG_SmartiecarV2\src\ino\smartiecar_libs\cam.h"

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
 ██████╗ ██╗    ██╗███╗   ██╗    ███████╗██╗   ██╗███╗   ██╗ ██████╗████████╗██╗ ██████╗ ███╗   ██╗███████╗
██╔═══██╗██║    ██║████╗  ██║    ██╔════╝██║   ██║████╗  ██║██╔════╝╚══██╔══╝██║██╔═══██╗████╗  ██║██╔════╝
██║   ██║██║ █╗ ██║██╔██╗ ██║    █████╗  ██║   ██║██╔██╗ ██║██║        ██║   ██║██║   ██║██╔██╗ ██║███████╗
██║   ██║██║███╗██║██║╚██╗██║    ██╔══╝  ██║   ██║██║╚██╗██║██║        ██║   ██║██║   ██║██║╚██╗██║╚════██║
╚██████╔╝╚███╔███╔╝██║ ╚████║    ██║     ╚██████╔╝██║ ╚████║╚██████╗   ██║   ██║╚██████╔╝██║ ╚████║███████║
 ╚═════╝  ╚══╝╚══╝ ╚═╝  ╚═══╝    ╚═╝      ╚═════╝ ╚═╝  ╚═══╝ ╚═════╝   ╚═╝   ╚═╝ ╚═════╝ ╚═╝  ╚═══╝╚══════╝
*/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// ProgramStop()
// stops the program and saves current time in millis
/////////////////////////////////////////////////////////////////////
void ProgramStop()
{
  unsigned long DrivingTime;
  stopMotor();
  // save time
  DrivingTime = millis() - start_time;
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("time: ");
  lcd.print(DrivingTime);
  delay(9999999);
}

/////////////////////////////////////////////////////////////////////
// Gyro_steer_straight()
// uses the gyro to orient itself straight to the track
////////////////////////////////////////////////////////////////////
void Gyro_steer_straight()
{
  float angle;
  int Speed;
  int Steering;
  angle = IMU_getAngle();

  Steering = (angle - StraightAngle) * 0.8; // 0.8 = wiggle factor
  if (Steering > 15.0)
  {
    Steering = 15;
  }
  else if (Steering < -15.0)
  {
    Steering = -15;
  }

  if (Steering < 0)
  {
    Steering = Steering * (-1);
    right(Steering);
  }

  else
  {
    left(Steering);
  }

  // delay(20);
}

/////////////////////////////////////////////////////////////////////
// CalculateTargetDirection()
// calculates the target direction for the next curve
/////////////////////////////////////////////////////////////////////
float CalculateTargetDirection()
{
  float angle;
  float CalculateAngle = 0.0;
  angle = CalculateStraightAngle();
  if (DrivingDirection == 'R')
  {
    CalculateAngle = angle + 90.0;
  }

  else if (DrivingDirection == 'L')
  {
    CalculateAngle = angle - 90.0;
  }

  return CalculateAngle;
}

/////////////////////////////////////////////////////
// CalculateStraightAngle()
// calculates the target angle for the next curve
/////////////////////////////////////////////////////
float CalculateStraightAngle()
{
  float StraightAngle = 0.0;
  float angle;
  int factor = 0;
  angle = IMU_getAngle();
  if (DrivingDirection == 'R')
  {
    // calculate straight direction
    factor = int((angle + 45.0) / 90.0); // int cuts off fractions
    StraightAngle = factor * 90.0;
  }

  else if (DrivingDirection == 'L')
  {
    // calculate straight direction
    factor = int((angle - 45.0) / 90.0); // int cuts off fractions
    StraightAngle = factor * 90.0;
  }

  return StraightAngle;
}

/////////////////////////////////////////////////////////////////////
// TurnLeft()
// turns left until 90 degrees
/////////////////////////////////////////////////////////////////////
void TurnLeft()
{
  float TargetDirection;
  int Speed;
  lcd.setRGB(125, 0, 125);
  Speed = SlowSpeed;
  right(40);
  runMotor_R(SlowSpeed); // backwards turn
  angle = IMU_getAngle();
  // replaces complicated quadrantSYS
  TargetDirection = CalculateTargetDirection();
  // turn until 90 degrees or block in sight
  while ((angle > TargetDirection - correction_Left))
  {

    angle = IMU_getAngle();
    //
    Speed = Speed + 5;
    if (Speed > CurveSpeed)
    {
      Speed = CurveSpeed;
    }
    runMotor_R(Speed);
    // delay(20);
  }
  stopMotor();
  corners = corners + 1;
  center();
  Distance_Left = SpaceUltraSonicLeft();
  Distance_Right = SpaceUltraSonicRight();
  lcd.setRGB(255, 255, 255);
  LastCurveTime = millis();
  runMotor(SlowSpeed);
}

/////////////////////////////////////////////////////////////////////
// TurnRight()
// turns right until 90 degrees
/////////////////////////////////////////////////////////////////////
void TurnRight()
{
  float TargetDirection;
  int Speed;
  lcd.setRGB(125, 0, 125);
  Speed = SlowSpeed;
  // back turn
  left(40);
  runMotor_R(SlowSpeed);
  angle = IMU_getAngle();
  // replaces complicated quadrantsystem
  TargetDirection = CalculateTargetDirection();
  // turn until 90 degrees or block in sight
  while (angle < TargetDirection - correction_Right)
  {
    // turn until 90 degrees or block in sight
    angle = IMU_getAngle();
    //
    Speed = Speed + 5;
    if (Speed > CurveSpeed)
    {
      Speed = CurveSpeed;
    }
    runMotor_R(Speed);
    // delay(20);
  }
  stopMotor();
  corners = corners + 1;
  center();
  Distance_Left = SpaceUltraSonicLeft();
  Distance_Right = SpaceUltraSonicRight();
  lcd.setRGB(255, 255, 255);
  LastCurveTime = millis();
  runMotor(SlowSpeed);
}

/////////////////////////////////////////////////////////////////////
// alignCenter()
// aligns to the center of the track
/////////////////////////////////////////////////////////////////////
void alignCenter()
{
  int Steering;
  Steering = (Distance_Left - Distance_Right) * 0.3;
  if (Steering > 30.0)
  {
    Steering = 30;
  }
  else if (Steering < -30.0)
  {
    Steering = -30;
  }

  if (Steering < 0)
  {
    Steering = Steering * (-1);
    right(Steering);
  }

  else
  {
    left(Steering);
  }

  delay(20);
}

/////////////////////////////////////////////////////////////////////
// alignLeft()
// aligns to the left wall
/////////////////////////////////////////////////////////////////////
void alignLeft()
{
  int Steering;
  Distance_Left = SpaceUltraSonicLeft();
  Steering = (Distance_Left - walldistance) * 0.9;
  if (Steering > 30.0)
  {
    Steering = 30;
  }
  else if (Steering < -30.0)
  {
    Steering = -30;
  }

  if (Steering < 0)
  {
    Steering = Steering * (-1);
    right(Steering);
  }

  else
  {
    left(Steering);
  }

  delay(20);
}

/////////////////////////////////////////////////////////////////////
// alignRight()
// aligns to the right wall
/////////////////////////////////////////////////////////////////////
void alignRight()
{
  int Steering;

  Distance_Right = SpaceUltraSonicRight();

  Steering = (walldistance - Distance_Right) * 0.9;
  if (Steering > 30.0)
  {
    Steering = 30;
  }
  else if (Steering < -30.0)
  {
    Steering = -30;
  }

  if (Steering < 0)
  {
    Steering = Steering * (-1);
    right(Steering);
  }

  else
  {
    left(Steering);
  }

  delay(20);
}

/////////////////////////////////////////////////////////////////////
// CurveLeftUntilBlock()
// makes left turn until 90 degrees or block in sight to adjust speed
/////////////////////////////////////////////////////////////////////
void CurveLeftUntilBlock()
{
  int Speed;
  float TargetDirection;
  Speed = SlowSpeed;
  lcd.setRGB(0, 0, 255);
  left(40);
  angle = IMU_getAngle();
  // replaces quadrantenSYS
  TargetDirection = CalculateTargetDirection();
  runMotor(SlowSpeed);
  // turn until 90 degrees or block in sight
  while (angle > TargetDirection - correction_Left)
  {
    angle = IMU_getAngle();
    Speed = Speed + 5;
    if (Speed > CurveSpeed)
    {
      Speed = CurveSpeed;
    }
    runMotor(Speed);
    delay(20);
  }
  center();
  corners = corners + 1;
  stopMotor();
  delay(20);
  runMotor_R(SlowSpeed);
  delay(250);
  stopMotor();
  runMotor(SlowSpeed);
  Distance_Left = SpaceUltraSonicLeft();
  Distance_Right = SpaceUltraSonicRight();
  lcd.setRGB(255, 255, 255);
  LastCurveTime = millis();
}

/////////////////////////////////////////////////////////////////////
// CurveRightUntilBlock()
// makes right turn until 90 degrees or block in sight to adjust speed
/////////////////////////////////////////////////////////////////////
void CurveRightUntilBlock()
{
  int Speed;
  float TargetDirection;
  Speed = SlowSpeed;
  lcd.setRGB(0, 0, 255);
  right(40);
  angle = IMU_getAngle();
  // replaces complicated quadrantSYS
  TargetDirection = CalculateTargetDirection();
  while (angle < TargetDirection - correction_Right)
  {
    angle = IMU_getAngle();
    Speed = Speed + 5;
    if (Speed > CurveSpeed)
    {
      Speed = CurveSpeed;
    }
    runMotor(Speed);
    delay(20);
  }
  center();
  corners = corners + 1;
  stopMotor();
  delay(20);
  runMotor_R(SlowSpeed);
  delay(250);
  stopMotor();
  runMotor(SlowSpeed);
  Distance_Left = SpaceUltraSonicLeft();
  Distance_Right = SpaceUltraSonicRight();
  lcd.setRGB(255, 255, 255);
  LastCurveTime = millis();
}

/////////////////////////////////////////////////////////////////////
// DriveUntilRedBlockInSight()
// drives towards red block and tries to drive right
/////////////////////////////////////////////////////////////////////
void DriveUntilRedBlockInSight()
{
  findNextPillar();
  if (P_x > Picturemiddle_x - 10)
  {
    right(25);
    while (P_x > Picturemiddle_x - 10)
    {
      delay(20);
      findNextPillar();
    }
  }
  else if (P_x < Picturemiddle_x)
  {
    left(25);
    while (P_x > Picturemiddle_x - 10)
    {
      delay(20);
      findNextPillar();
    }
  }
  center();
  Distance_Front = SpaceUltraSonicFront();
  while (Distance_Front > 30)
  {
    delay(50);
    Distance_Front = SpaceUltraSonicFront();
  }
}

/////////////////////////////////////////////////////////////////////
// DriveUntilGreenBlockInSight()
// drives towards green block and tries to drive left
/////////////////////////////////////////////////////////////////////
void DriveUntilGreenBlockInSight()
{
  findNextPillar();
  if (P_x > Picturemiddle_x - 10)
  {
    left(25);
    while (P_x > Picturemiddle_x - 10)
    {
      delay(20);
      findNextPillar();
    }
  }
  else if (P_x < Picturemiddle_x)
  {
    right(25);
    while (P_x > Picturemiddle_x - 10)
    {
      delay(20);
      findNextPillar();
    }
  }
  center();
  Distance_Front = SpaceUltraSonicFront();
  while (Distance_Front > 30)
  {
    delay(50);
    Distance_Front = SpaceUltraSonicFront();
  }
}

/////////////////////////////////////////////////////////////////////
// EvadeGreenPillar()
// evades green block to the left until it is not in sight
/////////////////////////////////////////////////////////////////////
void EvadeGreenPillar()
{
  float angle;
  float TargetDirection;
  int Speed;

  // save orientation and quadrant
  TargetDirection = IMU_getAngle();
  left(25);
  runMotor(CurveSpeed);
  // turn until block not in sight
  while (P_color == 'L')
  {
    delay(20);
    findNextPillar();
  }
  runMotor(SlowSpeed);
  center();

  delay(40); // straight after delay
  right(25);
  runMotor(CurveSpeed);
  // steer until orientation reached
  angle = IMU_getAngle();
  while (angle < TargetDirection)
  {
    angle = IMU_getAngle();
    delay(20);
  }
  center();
  runMotor(SlowSpeed);
  // drive past
  delay(200);
  lcd.setRGB(255, 255, 255);
}

/////////////////////////////////////////////////////////////////////
// EvadeRedPillar()
// evades red block to the right until it is not in sight
/////////////////////////////////////////////////////////////////////
void EvadeRedPillar()
{
  float angle;
  float TargetDirection;
  int Speed;

  // save orientation and quadrant
  TargetDirection = IMU_getAngle();
  right(25);
  runMotor(CurveSpeed);
  // drive until block not in sight
  while (P_color == 'R')
  {
    delay(20);
    findNextPillar();
  }
  runMotor(SlowSpeed);
  center();

  delay(50); // straight after delay
  left(25);
  runMotor(CurveSpeed);
  // TargetDirection = IMU_TD_gerade();
  angle = IMU_getAngle();
  while (angle > TargetDirection /*- correction_Left*/)
  {
    angle = IMU_getAngle();
    delay(20);
  }
  center();
  runMotor(SlowSpeed);
  // drive past
  delay(200);
  lcd.setRGB(255, 255, 255);
}

/////////////////////////////////////////////////////////////////////
// measureAllDistances()
// measures all distances and saves them in global variables
/////////////////////////////////////////////////////////////////////
void measureAllCurrentDistances()
{
  Distance_Front = SpaceUltraSonicFront();
  Distance_Left = SpaceUltraSonicLeft();
  Distance_Right = SpaceUltraSonicRight();
}

/////////////////////////////////////////////////////////////////////
// printAllDistancesToLCD()
// prints all distances to the LCD aswell as DrivingDirection
/////////////////////////////////////////////////////////////////////
void printAllCurrentDistancesToLCD()
{
  lcd.setCursor(0, 0);
  lcd.print(Distance_Left);
  lcd.print("  ");
  lcd.print(Distance_Front);
  lcd.print("  ");
  lcd.print(Distance_Right);
  lcd.setCursor(0, 1);
  lcd.print(angle);
  lcd.print("  ");
  lcd.print(DrivingDirection);
  lcd.print("  ");
  lcd.print(P_color);
}

/////////////////////////////////////////////////////////////////////
// waitOnButtonPress()
// waits until the button is pressed
/////////////////////////////////////////////////////////////////////
void waitOnButtonPress()
{
  // switch lcd to yellow and wait on button press
  lcd.setRGB(255, 130, 0);
  while (digitalRead(Button) == LOW)
  {
    delay(50);
  }
}

/////////////////////////////////////////////////////////////////////
// saveCurrentTime()
// saves the current time in milliseconds subtracting NextCurveDelay
/////////////////////////////////////////////////////////////////////

void saveCurrentTime()
{
  start_time = millis();
  LastCurveTime = millis() - NextCurveDelay;
}

///////////////////////////////////////////////////////////////////////
// evaluateRaspiData()
// evaluates the data from the raspi handshake
///////////////////////////////////////////////////////////////////////
void evaluateRaspiData()
{
  if (P_color != 'U')
  {
    if (P_x < 110)
    {
      DrivingDirection = 'L';
    }
    else if (P_x > 210)
    {
      DrivingDirection = 'R';
    }
  }
}

///////////////////////////////////////////////////////////
// startPhase()
// starts the phase
///////////////////////////////////////////////////////////
void startPhase()
{
  runMotor(SlowSpeed);

  if ((P_color == 'R') && (P_x < 210) && (P_x == 110))
  {
    EvadeRedPillar();
  }

  else if ((P_color == 'L') && (P_x > 210) && (P_x == 110))
  {
    EvadeGreenPillar();
  }

  else
  {
    while (Distance_Front > 10)
    {
      Gyro_steer_straight();
      Distance_Front = SpaceUltraSonicFront();
    }
    stopMotor();

    if (DrivingDirection == 'U')
    {
      Distance_Left = SpaceUltraSonicLeft();
      Distance_Right = SpaceUltraSonicRight();

      if (Distance_Left > 80)
      {
        DrivingDirection = 'L';
        TurnLeft();
      }

      else if (Distance_Right > 80)
      {
        DrivingDirection = 'R';
        TurnRight();
      }
    }
  }
}

///////////////////////////////////////////
/*
███████╗███████╗████████╗██╗   ██╗██████╗
██╔════╝██╔════╝╚══██╔══╝██║   ██║██╔══██╗
███████╗█████╗     ██║   ██║   ██║██████╔╝
╚════██║██╔══╝     ██║   ██║   ██║██╔═══╝
███████║███████╗   ██║   ╚██████╔╝██║
╚══════╝╚══════╝   ╚═╝    ╚═════╝ ╚═╝
*/
///////////////////////////////////////////

void setup()
{
  Wire.begin();

  pinMode(Button, INPUT); // button

  // set up the LCD's number of columns and rows:
  lcd.begin(16, 2);
  lcd.setRGB(255, 0, 0);
  lcd.print("obstacle race");

  // from Steering.h
  servosetup();

  // setup from ultrasonic_urm09.h
  ultrasonicstart();

  // initalises motor pinmodes from DCmotor.h
  motorsetup();

  delay(5000); // wait for 5 seconds

  // gyro setup gyro2.h
  startGyroscope();

  // raspi handshake
  raspi_handshake();

  // distance to wall
  measureAllCurrentDistances();

  findNextPillar();

  // count corners
  angle = IMU_getAngle();

  // prints all distances to LCD
  printAllCurrentDistancesToLCD();

  // waits for button press
  waitOnButtonPress();

  lcd.clear();

  // LCD goes White
  lcd.setRGB(255, 255, 255);

  // save time in millis
  saveCurrentTime();

  // Steering centered
  center();

  startPhase();

  stopMotor();
}
// Done

//////////////////////////////////
/*
██╗      ██████╗  ██████╗ ██████╗
██║     ██╔═══██╗██╔═══██╗██╔══██╗
██║     ██║   ██║██║   ██║██████╔╝
██║     ██║   ██║██║   ██║██╔═══╝
███████╗╚██████╔╝╚██████╔╝██║
╚══════╝ ╚═════╝  ╚═════╝ ╚═╝
 */
//////////////////////////////////

void loop()
{
}